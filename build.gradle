import io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository

plugins {
    id 'groovy'
    id 'io.github.gradle-nexus.publish-plugin'
}

version = projectVersion
ext.set('grailsVersion', libs.versions.grails.asProvider().get())
ext.set('isSnapshot', projectVersion.endsWith('-SNAPSHOT'))
ext.set('isReleaseVersion', !isSnapshot)

if(isReleaseVersion) {
    nexusPublishing {
        String nexusUser = project.findProperty('sonatypeOssUsername')
        String nexusPass = project.findProperty('sonatypeOssPassword')
        String nexusStagingProfileId = project.findProperty('sonatypeOssStagingProfileId')
        repositories {
            sonatype {
                nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
                username = nexusUser
                password = nexusPass
                stagingProfileId = nexusStagingProfileId
            }
        }
    }
}


subprojects {
    version = rootProject.version
    repositories {
        mavenLocal() // Used by Groovy Joint Workflow
        mavenCentral()
        maven { url = 'https://repo.grails.org/grails/core' }
        if (libs.versions.groovy.get().endsWith('-SNAPSHOT')) {
            maven {
                name = 'JFrog Groovy snapshot repo'
                url = 'https://groovy.jfrog.io/artifactory/libs-snapshot-local/'
            }
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

//do not generate extra load on Nexus with new staging repository if signing fails
tasks.withType(InitializeNexusStagingRepository).configureEach {
    shouldRunAfter tasks.withType(Sign)
}