buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-docs:$grailsVersion"
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
    }
}

configurations {
    documentation
}

dependencies {
    documentation("org.fusesource.jansi:jansi:1.14")
}

task clean(type: Delete) {
    delete(project.file("${buildDir}/docs"))
}

task groovydoc(type:org.groovy.lang.groovydoc.tasks.GroovydocTask) {
    docTitle = "${project.title} - ${project.version}"
    destinationDir = project.file("build/docs/api")
    notimestamp = true
    noversionstamp = true
    def files = []
    project.rootProject.subprojects
            .findAll { !it.name != 'docs' && !it.name.startsWith('examples') }
            .each { subproject ->
        if(subproject.file('src/main/groovy').exists()) {
            files += subproject.files("src/main/groovy")
        }
    }
    source = files
    classpath = configurations.documentation
    jvmArgs "-Xmx1024m"
}

task publishGuide(type: grails.doc.gradle.PublishGuide) {
    description = 'Generate Guide'
    targetDir = project.file("${buildDir}/docs")
    sourceRepo = "https://github.com/${githubSlug}/edit/${githubBranch}/src/main/docs"
    sourceDir = new File(projectDir, "src/main/docs")
    propertiesFiles = [ new File(rootProject.projectDir, "gradle.properties") ]
    asciidoc = true
    resourcesDir = project.file("${projectDir}/src/main/resources")
    properties = [
            'safe':'UNSAFE',
            'version': project.version,
            'api': '../api',
            'sourceDir':rootProject.projectDir.absolutePath,
            'sourcedir':rootProject.projectDir.absolutePath,
            'javaee': 'https://docs.oracle.com/javaee/7/api/',
            'javase': 'https://docs.oracle.com/javase/7/docs/api/',
            'groovyapi': 'http://docs.groovy-lang.org/latest/html/gapi/',
            'grailsapi': 'http://docs.grails.org/latest/api/',
            'gormapi': 'http://gorm.grails.org/latest/api/',
            'springapi': 'https://docs.spring.io/spring/docs/current/javadoc-api/'
    ]
    doLast {
        ant.move(file:"${project.buildDir}/docs/guide/single.html",
                tofile:"${project.buildDir}/docs/guide/index.html", overwrite:true)
        new File(project.buildDir, "docs/index.html").text = '''
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN" "http://www.w3.org/TR/html4/loose.dtd">
<html lang="en">
<head>
<meta http-equiv="refresh" content="0; url=guide/index.html" />
</head>

</body>
</html>
'''
    }
}

task docs(dependsOn:[groovydoc, publishGuide])

task assemble(dependsOn: docs, type:Zip) {
    baseName = "${project.name}-${project.version}"
    destinationDir = new File(project.buildDir, "distributions")
    from project.files("${buildDir}/docs")
}